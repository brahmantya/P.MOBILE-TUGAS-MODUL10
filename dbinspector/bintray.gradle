apply plugin: 'maven-publish'
apply plugin: "com.jfrog.bintray"
apply plugin: 'com.github.dcendents.android-maven'

group = dbinspector.group
version = dbinspector.version

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.kotlin.srcDirs
}

task javadocsJar(type: Jar, dependsOn: 'dokkaJavadoc') {
    archiveClassifier.set('javadoc')
    from dokkaJavadoc.outputDirectory
}

artifacts {
    archives javadocsJar
    archives sourcesJar
}

publishing {
    publications {
        infinum(MavenPublication) {
            groupId = group
            artifactId = 'dbinspector'

            pom {
                name = 'Android DbInspector'
                description = 'Android library for viewing in app databases.'
                url = 'https://github.com/infinum/android_dbinspector'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
            pom.withXml {
                def root = asNode()
                def dependenciesNode = root.appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}

bintray {
    Properties properties = new Properties()
    def bintrayProperties = rootProject.file('bintray.properties')
    if (bintrayProperties.exists()) {
        properties.load(new FileInputStream(bintrayProperties))
    } else {
        properties.setProperty("bintray.user", "")
        properties.setProperty("bintray.apikey", "")
    }

    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    pkg {
        repo = 'android'
        name = 'dbinspector'
        userOrg = 'infinum'
        desc = 'DbInspector package'
        websiteUrl = 'https://github.com/infinum/android_dbinspector'
        issueTrackerUrl = 'https://github.com/infinum/android_dbinspector/issues'
        vcsUrl = 'https://github.com/infinum/android_dbinspector.git'
        licenses = ['Apache-2.0']
        version {
            name = "${dbinspector.version}"
            released = new Date()
            vcsTag = "${dbinspector.version}"
        }
        publish = true
        override = true
        publicDownloadNumbers = true
    }

    configurations = ['archives']
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'jar'
                groupId 'com.infinum.dbinspector'
                artifactId 'dbinspector'

                name 'dbinspector'
                description 'DbInspector'
                url 'https://github.com/infinum/android_dbinspector'

                licenses {
                    license {
                        name 'Apache'
                        url 'https://github.com/infinum/android_dbinspector/LICENSE'
                    }
                }
                developers {
                    developer {
                        id "reisub"
                        name "Dino Kovač"
                    }
                    developer {
                        id 'knobtviker'
                        name 'Bojan Komljenović'
                        email 'bojan.komljenovic@infinum.com'
                    }
                }
                scm {
                    url "scm:git@github.com:infinum/android_dbinspector.git"
                    connection "scm:git@github.com:infinum/android_dbinspector.git"
                    developerConnection "scm:git@github.com:infinum/android_dbinspector.git"
                }
            }
        }
    }
}